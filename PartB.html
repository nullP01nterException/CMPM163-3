<html>
	<head>
		<title>Part B</title>
		<style>
			body { margin: 0; }
			canvas { width: 100%; height: 100% }
		</style>
	</head>
	<body>
		<script src="js/three.js"></script>

		<script id="vertexShader" type="x-shader/x-vertex">

			varying vec2 vUv; 
			void main()
			{
			    vUv = uv;

			    vec4 mvPosition = modelViewMatrix * vec4(position, 1.0 );
			    gl_Position = projectionMatrix * mvPosition;
			}

		</script>

		<script id="fragmentShader" type="x-shader/x-fragment">
    		uniform float iGlobalTime;
            uniform sampler2D iChannel0;
            uniform vec2 screenRes;

            varying vec2 vUv;

            const int MAX_MARCHING_STEPS = 255;
            const float MIN_DIST = 0.0;
            const float MAX_DIST = 100.0;
            const float EPSILON = 0.0001;

            mat3 rotateX(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3(1, 0, 0),
                    vec3(0, c, -s),
                    vec3(0, s, c)
                );
            }

            mat3 rotateY(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3(c, 0, s),
                    vec3(0, 1, 0),
                    vec3(-s, 0, c)
                );
            }

            mat3 rotateZ(float theta) {
                float c = cos(theta);
                float s = sin(theta);
                return mat3(
                    vec3(c, -s, 0),
                    vec3(s, c, 0),
                    vec3(0, 0, 1)
                );
            }

            float intersectSDF(float distA, float distB) {
                return max(distA, distB);
            }

            float unionSDF(float distA, float distB) {
                return min(distA, distB);
            }

            float differenceSDF(float distA, float distB) {
                return max(distA, -distB);
            }

            //makes cube
            float boxSDF(vec3 p, vec3 size) {
                vec3 d = abs(p) - (size / 2.0);
                float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
                float outsideDistance = length(max(d, 0.0));
                
                return insideDistance + outsideDistance;
            }

            //makes spheres
            float sphereSDF(vec3 p, float r) {
                return length(p) - r;
            }

            float cylinderSDF(vec3 p, float h, float r) {
                float inOutRadius = length(p.xy) - r;
                
                float inOutHeight = abs(p.z) - h/2.0;
                
                float insideDistance = min(max(inOutRadius, inOutHeight), 0.0);

                float outsideDistance = length(max(vec2(inOutRadius, inOutHeight), 0.0));
                
                return insideDistance + outsideDistance;
            }

            float sceneSDF(vec3 samplePoint) {    
                samplePoint = rotateY(iGlobalTime / 2.0) * samplePoint;
                
                float cylinderRadius = 0.4 + (1.0 - 0.4) * (1.0 + sin(1.7 * iGlobalTime)) / 2.0;
                float cylinder1 = cylinderSDF(samplePoint, 2.0, cylinderRadius);
                float cylinder2 = cylinderSDF(rotateX(radians(90.0)) * samplePoint, 2.0, cylinderRadius);
                float cylinder3 = cylinderSDF(rotateY(radians(90.0)) * samplePoint, 2.0, cylinderRadius);
                
                float cube = boxSDF(samplePoint, vec3(1.8, 1.8, 1.8));
                
                float sphere = sphereSDF(samplePoint, 1.2);
                
                float ballOffset = 0.4 + 1.0 + sin(1.7 * iGlobalTime);
                float ballRadius = 0.3;
                float balls = sphereSDF(samplePoint - vec3(ballOffset, 0.0, 0.0), ballRadius);
                balls = unionSDF(balls, sphereSDF(samplePoint + vec3(ballOffset, 0.0, 0.0), ballRadius));
                
                float csgNut = differenceSDF(intersectSDF(cube, sphere),
                                     unionSDF(cylinder1, unionSDF(cylinder2, cylinder3)));
                
                return unionSDF(balls, csgNut);
            }

            float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
                float depth = start;
                for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
                    float dist = sceneSDF(eye + depth * marchingDirection);
                    if (dist < EPSILON) {
                        return depth;
                    }
                    depth += dist;
                    if (depth >= end) {
                        return end;
                    }
                }
                return end;
            }
            
            vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
                vec2 xy = fragCoord - size / 2.0;
                float z = size.y / tan(radians(fieldOfView) / 2.0);
                return normalize(vec3(xy, -z));
            }

            vec3 estimateNormal(vec3 p) {
                return normalize(vec3(
                    sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
                    sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
                    sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
                ));
            }

            vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye,
                                      vec3 lightPos, vec3 lightIntensity) {
                vec3 N = estimateNormal(p);
                vec3 L = normalize(lightPos - p);
                vec3 V = normalize(eye - p);
                vec3 R = normalize(reflect(-L, N));
                
                float dotLN = dot(L, N);
                float dotRV = dot(R, V);
                
                if (dotLN < 0.0) {
                    return vec3(0.0, 0.0, 0.0);
                } 
                
                if (dotRV < 0.0) {
                    return lightIntensity * (k_d * dotLN);
                }
                return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
            }

            vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
                const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
                vec3 color = ambientLight * k_a;
                
                vec3 light1Pos = vec3(4.0 * sin(iGlobalTime),
                                      2.0,
                                      4.0 * cos(iGlobalTime));
                vec3 light1Intensity = vec3(0.4, 0.4, 0.4);
                
                color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                              light1Pos,
                                              light1Intensity);
                
                vec3 light2Pos = vec3(2.0 * sin(0.37 * iGlobalTime),
                                      2.0 * cos(0.37 * iGlobalTime),
                                      2.0);
                vec3 light2Intensity = vec3(0.4, 0.4, 0.4);
                
                color += phongContribForLight(k_d, k_s, alpha, p, eye,
                                              light2Pos,
                                              light2Intensity);

                return color;
            }

            mat3 viewM(vec3 eye, vec3 center, vec3 up) {
                vec3 f = normalize(center - eye);
                vec3 s = normalize(cross(f, up));
                vec3 u = cross(s, f);
                return mat3(s, u, -f);
            }

            void main()
            {
                vec4 tex1 = texture2D(iChannel0, screenRes);
                vec3 viewDir = rayDirection(45.0, screenRes, gl_FragCoord.xy);
                vec3 eye = vec3(8.0, 5.0 * sin(0.2 * iGlobalTime), 7.0);
                
                mat3 viewToWorld = viewM(eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
                
                vec3 worldDir = viewToWorld * viewDir;
                
                float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);
                
                if (dist > MAX_DIST - EPSILON) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
                    return;
                }

                vec3 p = eye + dist * worldDir;
                vec3 K_a = (estimateNormal(p) + vec3(1.0)) / 2.0;
                vec3 K_d = K_a;
                vec3 K_s = vec3(1.0, 1.0, 1.0);
                float shininess = 10.0;
                
                vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
                vec3 outcol = vec3(mix(vec3(tex1), color, tan(iGlobalTime)));
                
                gl_FragColor = vec4(outcol, 1.0);
            }
		</script>

		<script>
			// resize canvas function

			var scene, camera, renderer;
			scene = new THREE.Scene();
			var fov = 90, aspect = window.innerWidth / window.innerHeight;
			camera = new THREE.PerspectiveCamera(fov, aspect, 0.1, 1000);
			camera.position.z = 10;
			camera.lookAt(scene.position);
			renderer = new THREE.WebGLRenderer();
			renderer.setClearColor(0xFACADE);
			renderer.setSize(window.innerWidth, window.innerHeight);
			document.body.appendChild(renderer.domElement);
			var clock = new THREE.Clock();

			var vshader = document.getElementById("vertexShader").textContent;
			var fshader = document.getElementById("fragmentShader").textContent;

			var texture1 = new THREE.TextureLoader().load( 'images/angus.jpg' );

			var tuniform = {
		        iGlobalTime:    { type: 'f', value: 0.1 },
		        iChannel0:  { type: 't', value:  texture1 },
                screenRes:  { type: 'v2', value: new THREE.Vector2() }
		    };
		    tuniform.iChannel0.value.wrapS = tuniform.iChannel0.value.wrapT = THREE.RepeatWrapping;
            tuniform.screenRes.value.x = window.innerWidth;
            tuniform.screenRes.value.y = window.innerHeight;

            window.addEventListener('resize',function() {
                tuniform.screenRes.value.x = window.innerWidth;
                tuniform.screenRes.value.y = window.innerHeight;
                camera.aspect = window.innerWidth / window.innerHeight;
                camera.updateProjectionMatrix();
                renderer.setSize(window.innerWidth, window.innerHeight);
            });

    		var mat = new THREE.ShaderMaterial( {
	            uniforms: tuniform,
	            vertexShader: vshader,
	            fragmentShader: fshader,            
	            side:THREE.DoubleSide
	        } );

	        var tobject = new THREE.Mesh( new THREE.PlaneGeometry(window.innerWidth, window.innerHeight, 1, 1), mat);
            tobject.position.set(0,0,0);
	        scene.add(tobject);
	        var clock = new THREE.Clock();

			function render() {
				tuniform.iGlobalTime.value += clock.getDelta();
				requestAnimationFrame(render);
				renderer.render(scene, camera);
			}
			render();
		</script>
	</body>
</html>

